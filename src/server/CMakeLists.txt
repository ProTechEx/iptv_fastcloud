# service variables
SET(STREAMER_SERVICE_NAME ${PROJECT_NAME_LOWERCASE})
SET(STREAMER_SERVICE_PORT 6317)
SET(STREAMER_SERVICE_HOST "localhost:${STREAMER_SERVICE_PORT}")
SET(STREAMER_SERVICE_HTTP_PORT 8000)
SET(STREAMER_SERVICE_HTTP_HOST "localhost:${STREAMER_SERVICE_HTTP_PORT}")
SET(STREAMER_SERVICE_VODS_PORT 7000)
SET(STREAMER_SERVICE_VODS_HOST "localhost:${STREAMER_SERVICE_VODS_PORT}")
SET(STREAMER_SERVICE_CODS_PORT 6000)
SET(STREAMER_SERVICE_CODS_HOST "localhost:${STREAMER_SERVICE_CODS_PORT}")
IF(SUBSCRIBERS)
  SET(STREAMER_SERVICE_SUBSCRIBERS_PORT 5000)
  SET(STREAMER_SERVICE_SUBSCRIBERS_HOST "localhost:${STREAMER_SERVICE_SUBSCRIBERS_PORT}")
  SET(STREAMER_SERVICE_BANDWIDTH_PORT 4000)
  SET(STREAMER_SERVICE_BANDWIDTH_HOST "localhost:${STREAMER_SERVICE_BANDWIDTH_PORT}")
ENDIF(SUBSCRIBERS)
SET(STREAMER_SERVICE_TTL_FILES 3600)
SET(STREAMER_SERVICE_STREAMLINK_PATH "/usr/local/bin/streamlink")
SET(STREAMER_SERVICE_NAME_EXE ${STREAMER_SERVICE_NAME}_s)

FIND_PACKAGE(Common REQUIRED)
FIND_PACKAGE(FastoTvProtocol REQUIRED)
FIND_PACKAGE(JSON-C REQUIRED)

# platform specific
IF(OS_WINDOWS)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES)
  SET(RELATIVE_SOURCE_DIR .)
ELSEIF(OS_LINUX)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES udev dl atomic)
  SET(RELATIVE_SOURCE_DIR ..)
ELSEIF(OS_MACOSX)
  FIND_LIBRARY(FOUNDATION_LIBRARY Foundation)
  FIND_LIBRARY(IOKIT_LIBRARY IOKit)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES dl ${FOUNDATION_LIBRARY} ${IOKIT_LIBRARY})
  SET(RELATIVE_SOURCE_DIR ..)
ELSEIF(OS_POSIX)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES udev dl)
  SET(RELATIVE_SOURCE_DIR ..)
ENDIF(OS_WINDOWS)

IF(USE_PTHREAD)
  IF(NOT OS_ANDROID)
   SET(PLATFORM_LIBRARIES ${PLATFORM_LIBRARIES} pthread)
  ENDIF(NOT OS_ANDROID)
ENDIF(USE_PTHREAD)

SET(PIPE_HEADERS ${CMAKE_SOURCE_DIR}/src/server/pipe/pipe_client.h)
SET(PIPE_SOURCES ${CMAKE_SOURCE_DIR}/src/server/pipe/pipe_client.cpp)

SET(OPTIONS_HEADERS ${CMAKE_SOURCE_DIR}/src/server/options/options.h)
SET(OPTIONS_SOURCES ${CMAKE_SOURCE_DIR}/src/server/options/options.cpp)

SET(SERVER_HTTP_HEADERS
  ${CMAKE_SOURCE_DIR}/src/server/http/handler.h
  ${CMAKE_SOURCE_DIR}/src/server/http/client.h
  ${CMAKE_SOURCE_DIR}/src/server/http/server.h
)

SET(SERVER_HTTP_SOURCES
  ${CMAKE_SOURCE_DIR}/src/server/http/handler.cpp
  ${CMAKE_SOURCE_DIR}/src/server/http/client.cpp
  ${CMAKE_SOURCE_DIR}/src/server/http/server.cpp
)

SET(SERVER_VODS_HEADERS
  ${CMAKE_SOURCE_DIR}/src/server/vods/handler.h
  ${CMAKE_SOURCE_DIR}/src/server/vods/client.h
  ${CMAKE_SOURCE_DIR}/src/server/vods/server.h
)

SET(SERVER_VODS_SOURCES
  ${CMAKE_SOURCE_DIR}/src/server/vods/handler.cpp
  ${CMAKE_SOURCE_DIR}/src/server/vods/client.cpp
  ${CMAKE_SOURCE_DIR}/src/server/vods/server.cpp
)

IF(SUBSCRIBERS)
  SET(SERVER_SUBSCRIBERS_HEADERS
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/handler.h
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/client.h
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/server.h
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/server_auth_info.h
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/commands_info/user_info.h
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/isubscribe_finder.h
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/rpc/user_rpc_info.h

    ${CMAKE_SOURCE_DIR}/src/server/sync_finder.h
  )

  SET(SERVER_SUBSCRIBERS_SOURCES
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/handler.cpp
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/client.cpp
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/server.cpp
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/server_auth_info.cpp
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/commands_info/user_info.cpp
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/isubscribe_finder.cpp
    ${CMAKE_SOURCE_DIR}/src/server/subscribers/rpc/user_rpc_info.cpp

    ${CMAKE_SOURCE_DIR}/src/server/sync_finder.cpp
  )
ENDIF(SUBSCRIBERS)

SET(SERVER_DAEMON_HEADERS
  ${CMAKE_SOURCE_DIR}/src/server/daemon/client.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/server.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_factory.h

  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/details/shots.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/sync_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/activate_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/license_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/stop_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/ping_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/server_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/prepare_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/get_log_info.h

  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/stream_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/start_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/quit_status_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/restart_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/stop_info.h
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/get_log_info.h
)

SET(SERVER_DAEMON_SOURCES
  ${CMAKE_SOURCE_DIR}/src/server/daemon/client.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/server.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_factory.cpp

  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service//details/shots.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/sync_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/license_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/activate_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/stop_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/ping_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/server_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/prepare_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/service/get_log_info.cpp

  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/stream_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/start_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/quit_status_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/restart_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/stop_info.cpp
  ${CMAKE_SOURCE_DIR}/src/server/daemon/commands_info/stream/get_log_info.cpp
)

SET(SERVER_HEADERS
  ${CMAKE_SOURCE_DIR}/src/server/base/iserver_handler.h
  ${CMAKE_SOURCE_DIR}/src/server/base/ihttp_requests_observer.h

  ${CMAKE_SOURCE_DIR}/src/server/child.h
  ${CMAKE_SOURCE_DIR}/src/server/child_stream.h
  ${CMAKE_SOURCE_DIR}/src/server/process_slave_wrapper.h
  ${CMAKE_SOURCE_DIR}/src/server/config.h

  ${SERVER_HTTP_HEADERS}
  ${SERVER_VODS_HEADERS}
  ${SERVER_SUBSCRIBERS_HEADERS}
  ${SERVER_DAEMON_HEADERS}
  ${PIPE_HEADERS}
  ${OPTIONS_HEADERS}
)
SET(SERVER_SOURCES
  ${CMAKE_SOURCE_DIR}/src/server/base/iserver_handler.cpp
  ${CMAKE_SOURCE_DIR}/src/server/base/ihttp_requests_observer.cpp

  ${CMAKE_SOURCE_DIR}/src/server/child.cpp
  ${CMAKE_SOURCE_DIR}/src/server/child_stream.cpp
  ${CMAKE_SOURCE_DIR}/src/server/process_slave_wrapper.cpp
  ${CMAKE_SOURCE_DIR}/src/server/config.cpp

  ${SERVER_HTTP_SOURCES}
  ${SERVER_VODS_SOURCES}
  ${SERVER_SUBSCRIBERS_SOURCES}
  ${SERVER_DAEMON_SOURCES}
  ${PIPE_SOURCES}
  ${OPTIONS_SOURCES}
)

SET(PERF_OBSERVER_HEADERS
  ${CMAKE_SOURCE_DIR}/src/server/gpu_stats/perf_monitor.h
)
SET(PERF_OBSERVER_SOURCES
  ${CMAKE_SOURCE_DIR}/src/server/gpu_stats/perf_monitor.cpp
)

# HARDWARE specific
# hw algo
IF(NOT DEFINED LICENSE_ALGO)
  SET(LICENSE_ALGO 0)
ENDIF(NOT DEFINED LICENSE_ALGO)

SET(LICENSE_KEY "" CACHE STRING "Hardware specific key")
STRING(LENGTH ${LICENSE_KEY} LICENSE_KEY_LENGHT)
SET(LICENSE_KEY_LENGHT_MUSTBE 64)
IF(NOT LICENSE_KEY_LENGHT EQUAL ${LICENSE_KEY_LENGHT_MUSTBE})
  MESSAGE(FATAL_ERROR "License key is invalid(lenght=${LICENSE_KEY_LENGHT} but must be ${LICENSE_KEY_LENGHT_MUSTBE}), please use license_gen command to get key!")
ENDIF(NOT LICENSE_KEY_LENGHT EQUAL ${LICENSE_KEY_LENGHT_MUSTBE})

MESSAGE(STATUS "LICENSE_ALGO: ${LICENSE_ALGO}")
MESSAGE(STATUS "LICENSE_KEY: ${LICENSE_KEY}")

#gpu nvidia
SET(NVML_LIB_PATHS /usr/src/gdk/nvml/lib /usr/lib/nvidia-375)
SET(NVML_INCLUDE_PATHS /usr/include/nvidia/gdk /usr/local/cuda/include /usr/local/cuda-8.0/include)
FIND_LIBRARY(NVML_LIBRARY NAMES nvidia-ml PATHS ${NVML_LIB_PATHS})
FIND_PATH(NVML_INCLUDE_DIRS NAMES nvml.h PATHS ${NVML_INCLUDE_PATHS})
MESSAGE("NVML_LIBRARY: ${NVML_LIBRARY}, NVML_INCLUDE_DIRS: ${NVML_INCLUDE_DIRS}")
IF(NVML_LIBRARY AND NVML_INCLUDE_DIRS)
  SET(DAEMON_LIBRARIES ${DAEMON_LIBRARIES} ${NVML_LIBRARY})
  SET(PERF_OBSERVER_HEADERS ${PERF_OBSERVER_HEADERS} ${CMAKE_SOURCE_DIR}/src/server/gpu_stats/nvidia_monitor.h)
  SET(PERF_OBSERVER_SOURCES ${PERF_OBSERVER_SOURCES} ${CMAKE_SOURCE_DIR}/src/server/gpu_stats/nvidia_monitor.cpp)
  SET(PRIVATE_INCLUDE_DIRECTORIES_SLAVE ${PRIVATE_INCLUDE_DIRECTORIES_SLAVE} ${NVML_INCLUDE_DIRS})
  SET(PRIVATE_COMPILE_DEFINITIONS_SLAVE ${PRIVATE_COMPILE_DEFINITIONS_SLAVE} -DHAVE_NVML)

  # ibnvidia-ml.so.1
  GET_FILENAME_COMPONENT(NVML_LIBRARY_WITHOUT_SYMLINK ${NVML_LIBRARY} REALPATH)
  GET_FILENAME_COMPONENT(NVML_LIBRARY_NAME ${NVML_LIBRARY_WITHOUT_SYMLINK} NAME)
  STRING(REGEX REPLACE "[^so]+$" ".1" NVML_LNNAME ${NVML_LIBRARY_NAME})
  #libSDL2-2.0.so.0
  INSTALL(FILES ${NVML_LIBRARY_WITHOUT_SYMLINK} DESTINATION ${LIB_INSTALL_DESTINATION} RENAME ${NVML_LNNAME} COMPONENT RUNTIME)
ENDIF(NVML_LIBRARY AND NVML_INCLUDE_DIRS)

#gpu intel
SET(CTT_METRICS_PATH "/opt/intel/mediasdk/tools/metrics_monitor")
FIND_LIBRARY(CTT_METRICS_LIBRARY
  NAMES cttmetrics
  PATHS ${CTT_METRICS_PATH}
  PATH_SUFFIXES _bin
)
FIND_PATH(CTT_METRICS_INCLUDE_DIRS
  NAMES cttmetrics.h
  PATHS ${CTT_METRICS_PATH}
  PATH_SUFFIXES include
)
MESSAGE("CTT_METRICS_LIBRARY: ${CTT_METRICS_LIBRARY}, CTT_METRICS_INCLUDE_DIRS: ${CTT_METRICS_INCLUDE_DIRS}")
IF (CTT_METRICS_LIBRARY AND CTT_METRICS_INCLUDE_DIRS)
  SET(DAEMON_LIBRARIES ${DAEMON_LIBRARIES} ${CTT_METRICS_LIBRARY})
  SET(PERF_OBSERVER_HEADERS ${PERF_OBSERVER_HEADERS} ${CMAKE_SOURCE_DIR}/src/server/gpu_stats/intel_monitor.h)
  SET(PERF_OBSERVER_SOURCES ${PERF_OBSERVER_SOURCES} ${CMAKE_SOURCE_DIR}/src/server/gpu_stats/intel_monitor.cpp)
  SET(PRIVATE_INCLUDE_DIRECTORIES_SLAVE ${PRIVATE_INCLUDE_DIRECTORIES_SLAVE} ${CTT_METRICS_INCLUDE_DIRS})
  SET(PRIVATE_COMPILE_DEFINITIONS_SLAVE ${PRIVATE_COMPILE_DEFINITIONS_SLAVE} -DHAVE_CTT_METRICS)
  INSTALL(FILES ${CTT_METRICS_LIBRARY} DESTINATION ${LIB_INSTALL_DESTINATION} COMPONENT RUNTIME)
ENDIF(CTT_METRICS_LIBRARY AND CTT_METRICS_INCLUDE_DIRS)

IF(OS_POSIX)
  SET(SERVER_SOURCES ${SERVER_SOURCES} ${CMAKE_SOURCE_DIR}/src/server/process_slave_wrapper_posix.cpp)
ENDIF(OS_POSIX)

SET(DAEMON_SOURCES
  ${SERVER_HEADERS} ${SERVER_SOURCES}
  ${PERF_OBSERVER_HEADERS} ${PERF_OBSERVER_SOURCES}
)
SET(DAEMON_LIBRARIES
  ${DAEMON_LIBRARIES}
  ${FASTOTV_PROTOCOL_LIBRARIES}
  ${COMMON_LIBRARIES}
  ${JSONC_LIBRARIES}
  ${PLATFORM_LIBRARIES}
  ${STREAMER_COMMON} #FIXME
)
SET(PRIVATE_INCLUDE_DIRECTORIES_SLAVE
  ${PRIVATE_INCLUDE_DIRECTORIES_SLAVE}
  ${FASTOTV_PROTOCOL_INCLUDE_DIRS}
  ${COMMON_INCLUDE_DIRS}
  ${CMAKE_SOURCE_DIR}/src
)
SET(CORE_LIBRARY ${LIB_INSTALL_DESTINATION}/${CMAKE_SHARED_LIBRARY_PREFIX}${STREAMER_CORE}${CMAKE_SHARED_LIBRARY_SUFFIX})

SET(RUN_DIR_PATH "/var/run/${STREAMER_SERVICE_NAME}")
SET(PIDFILE_PATH "${RUN_DIR_PATH}/${STREAMER_SERVICE_NAME}.pid")
SET(USER_NAME ${PROJECT_NAME_LOWERCASE})
SET(USER_GROUP ${PROJECT_NAME_LOWERCASE})
SET(CONFIG_PATH "/etc/${STREAMER_SERVICE_NAME}.conf")

SET(PRIVATE_COMPILE_DEFINITIONS_SLAVE
  ${PRIVATE_COMPILE_DEFINITIONS_SLAVE}
  -DUSER_NAME="${USER_NAME}"
  -DUSER_GROUP="${USER_GROUP}"
  -DCONFIG_PATH="${CONFIG_PATH}"
  -DLICENSE_ALGO=${LICENSE_ALGO}
  -DLICENSE_KEY="${LICENSE_KEY}"
  -DPIDFILE_PATH="${PIDFILE_PATH}"
  -DCORE_LIBRARY="${CORE_LIBRARY}"
  -DPIDFILE_PATH="${PIDFILE_PATH}"
  -DSTREAMER_NAME="${STREAMER_NAME}"
  -DSTREAMER_SERVICE_NAME="${STREAMER_SERVICE_NAME}"
  -DRELATIVE_SOURCE_DIR="${RELATIVE_SOURCE_DIR}"
  -DCLIENT_PORT=${STREAMER_SERVICE_PORT}
  -DHTTP_PORT=${STREAMER_SERVICE_HTTP_PORT}
  -DVODS_PORT=${STREAMER_SERVICE_VODS_PORT}
  -DCODS_PORT=${STREAMER_SERVICE_CODS_PORT}
  -DBANDWIDTH_PORT=${STREAMER_SERVICE_BANDWIDTH_PORT}
  -DTTL_FILES=${STREAMER_SERVICE_TTL_FILES}
  -DSTREAMER_SERVICE_STREAMLINK_PATH="${STREAMER_SERVICE_STREAMLINK_PATH}"
)

IF(SUBSCRIBERS)
  SET(PRIVATE_COMPILE_DEFINITIONS_SLAVE ${PRIVATE_COMPILE_DEFINITIONS_SLAVE}
    -DSUBSCRIPERS_PORT=${STREAMER_SERVICE_SUBSCRIBERS_PORT}
  )
ENDIF(SUBSCRIBERS)

SET(EXE_DAEMON_SOURCES ${CMAKE_SOURCE_DIR}/src/server/daemon_slave.cpp)

ADD_EXECUTABLE(${STREAMER_SERVICE_NAME_EXE} ${DAEMON_SOURCES} ${EXE_DAEMON_SOURCES})
TARGET_INCLUDE_DIRECTORIES(${STREAMER_SERVICE_NAME_EXE} PRIVATE ${PRIVATE_INCLUDE_DIRECTORIES_SLAVE})
TARGET_COMPILE_DEFINITIONS(${STREAMER_SERVICE_NAME_EXE} PRIVATE ${PRIVATE_COMPILE_DEFINITIONS_SLAVE})
TARGET_LINK_LIBRARIES(${STREAMER_SERVICE_NAME_EXE} ${DAEMON_LIBRARIES})

IF(PROJECT_BUILD_TYPE_VERSION STREQUAL "release")
  STRIP_TARGET(${STREAMER_SERVICE_NAME_EXE})
ENDIF(PROJECT_BUILD_TYPE_VERSION STREQUAL "release")
INSTALL(TARGETS ${STREAMER_SERVICE_NAME_EXE} DESTINATION ${TARGET_INSTALL_DESTINATION} COMPONENT APPLICATIONS)

SET(EXECUTABLE_FOLDER_PATH ${CMAKE_INSTALL_PREFIX}/bin)

IF(OS_WINDOWS)
ELSEIF(OS_MACOSX)
ELSEIF(OS_LINUX OR OS_FREEBSD)
  SET(EXECUTABLE_PATH ${EXECUTABLE_FOLDER_PATH}/${STREAMER_SERVICE_NAME} CACHE INTERNAL "Daemon path: ${EXECUTABLE_PATH}")
ENDIF(OS_WINDOWS)

# script
SET(SERVICE_START_SCRIPT_GEN_PATH ${CMAKE_BINARY_DIR}/service/${STREAMER_SERVICE_NAME})
GEN_START_SCRIPT(${SERVICE_START_SCRIPT_GEN_PATH} ${STREAMER_SERVICE_NAME_EXE})
INSTALL(PROGRAMS ${SERVICE_START_SCRIPT_GEN_PATH} DESTINATION
  ${TARGET_INSTALL_DESTINATION} COMPONENT APPLICATIONS
)

# service
IF(OS_LINUX OR OS_FREEBSD)
  SET(SERVICE_SCRIPT_GEN_PATH ${CMAKE_BINARY_DIR}/service/${STREAMER_SERVICE_NAME}.service)
  GEN_SERVICE_SERVICE_FILE(${SERVICE_SCRIPT_GEN_PATH}
    ${STREAMER_SERVICE_NAME} ${STREAMER_SERVICE_NAME_EXE}
    ${EXECUTABLE_PATH}
    ${RUN_DIR_PATH}
    ${PIDFILE_PATH}
    ${USER_NAME} ${USER_GROUP}
    ${PROJECT_SUMMARY}
  )
  INSTALL(FILES ${SERVICE_SCRIPT_GEN_PATH} DESTINATION /etc/systemd/system/)

  SET(SERVICE_DEB_SCRIPT_GEN_PATH ${CMAKE_BINARY_DIR}/service/${STREAMER_SERVICE_NAME}.debian)
  GEN_DEBIAN_SERVICE_FILE(${SERVICE_DEB_SCRIPT_GEN_PATH}
    ${STREAMER_SERVICE_NAME} ${STREAMER_SERVICE_NAME_EXE}
    ${EXECUTABLE_PATH}
    ${RUN_DIR_PATH}
    ${PIDFILE_PATH}
    ${USER_NAME} ${USER_GROUP}
    ${PROJECT_SUMMARY}
  )
  INSTALL(FILES ${SERVICE_DEB_SCRIPT_GEN_PATH}
    DESTINATION /etc/init.d
    RENAME ${STREAMER_SERVICE_NAME}
    PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)
ENDIF(OS_LINUX OR OS_FREEBSD)

# config file
SET(SERVICE_CONF_GEN_PATH ${CMAKE_BINARY_DIR}/service/${STREAMER_SERVICE_NAME}.conf)
CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/install/${PROJECT_NAME_LOWERCASE}/service/service.conf.in
  ${SERVICE_CONF_GEN_PATH} @ONLY IMMEDIATE
)
IF(NOT EXISTS ${CONFIG_PATH})
  INSTALL(FILES ${SERVICE_CONF_GEN_PATH} DESTINATION /etc/)
ENDIF(NOT EXISTS ${CONFIG_PATH})

IF (DEVELOPER_CHECK_STYLE)
  SET(CHECK_SOURCES_DAEMON ${DAEMON_SOURCES} ${EXE_DAEMON_SOURCES})
  REGISTER_CHECK_STYLE_TARGET(check_style_${STREAMER_SERVICE_NAME} "${CHECK_SOURCES_DAEMON}")
  REGISTER_CHECK_INCLUDES_TARGET(${STREAMER_SERVICE_NAME_EXE})
ENDIF(DEVELOPER_CHECK_STYLE)


IF(DEVELOPER_ENABLE_TESTS)
  FIND_PACKAGE(GTest REQUIRED)

  ## Unit tests
  SET(PRIVATE_INCLUDE_DIRECTORIES_UNIT_TESTS
    ${PRIVATE_INCLUDE_DIRECTORIES_UNIT_TESTS}
    ${CMAKE_SOURCE_DIR}/src
  )
  SET(UNIT_TESTS_LIBS
      ${GTEST_BOTH_LIBRARIES}
      ${STREAMER_COMMON}
      ${PLATFORM_LIBRARIES})
  SET(UNIT_TESTS unit_tests_server)
  ADD_EXECUTABLE(${UNIT_TESTS}
    ${CMAKE_SOURCE_DIR}/tests/server/unit_test_server.cpp ${OPTIONS_SOURCES}
  )
  TARGET_INCLUDE_DIRECTORIES(${UNIT_TESTS} PRIVATE ${PRIVATE_INCLUDE_DIRECTORIES_UNIT_TESTS} ${JSONC_INCLUDE_DIRS})
  TARGET_LINK_LIBRARIES(${UNIT_TESTS} ${UNIT_TESTS_LIBS} ${DAEMON_LIBRARIES})
  ADD_TEST_TARGET(${UNIT_TESTS})
  SET_PROPERTY(TARGET ${UNIT_TESTS} PROPERTY FOLDER "Unit tests")
ENDIF(DEVELOPER_ENABLE_TESTS)
